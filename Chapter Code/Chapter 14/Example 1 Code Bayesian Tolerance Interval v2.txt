/* Example 1 SAS Code */

/* The SAS code below generates a one-sided 95% confidence, 99% coverage upper tolerance 
   limit for a Normal distribution, using both frequentist and Bayesian methodology. The 
   data are in SAS dataset Data1. The PROC CAPABILITY code generates the frequentist UTL, 
   and the remainder of the code generates and displays the Bayesian UTL. The METHOD=3 
   option in PROC CAPABILITY specifies a tolerance limit. The SOLUTION option is required 
   in the PROC MIXED code, which places a sample of 10,000 observations from the posterior 
   distribution into the SAS dataset Posterior1. The subsequent DATA step calculates 
   10,000 observations from the posterior distribution for the 99th percentile (P99) of 
   the underlying Normal distribution. The SAS variable name for the mean is beta1, and 
   the SAS variable name for the variance is covp1; these names are generated by PROC MIXED. */

DATA Data1 ;
INPUT Response ;
DATALINES ;
 99.180
 96.153
 94.766
100.242
100.202
103.021
103.240
100.824
105.822
103.627
 93.996
 98.314
104.935
100.319
 93.172
 95.237
107.932
 98.709
102.740
 95.225
RUN ;

PROC CAPABILITY DATA=Data1 ;
INTERVALS Response / TYPE=UPPER P=0.99 ALPHA=0.05 METHOD=3 ;
TITLE "Frequentist UTL for Example 1" ;
RUN ;

PROC MIXED DATA=Data1 ;
MODEL Response = / SOLUTION ;
PRIOR / OUT=Posterior1 NSAMPLE=10000 SEED=6456443 ;
TITLE "Example 1 Model Fit and Posterior Sample Generation" ;
RUN ;

PROC PRINT DATA=Posterior1 (OBS=20) ;
TITLE "First 20 Rows of Posterior1" ;
RUN ;

PROC UNIVARIATE DATA=Posterior1;
VAR beta1 covp1 ;
HISTOGRAM ;
INSET n median (6.2) mean (6.2) min (6.2) max (6.2) 
	/ POS=ne HEADER='Summary Statistics' ;
TITLE "Example 1 Diagnostics" ;
RUN ;


PROC SGPLOT DATA=Posterior1;
SCATTER x=sample y=beta1;
TITLE "Example 1 Diagnostics" ;
RUN;

PROC SGPLOT DATA=Posterior1;
SCATTER x=sample y=covp1;
TITLE "Example 1 Diagnostics" ;
RUN;


DATA Posterior ;
SET Posterior1 ;
SEED = 4896235 ;
z99 = PROBIT(0.99) ;
Mu = beta1 ;
Sigma = SQRT(covp1) ;
P99 = Mu + z99*Sigma ;
RUN ;

PROC UNIVARIATE DATA=Posterior ;
VAR P99 ;
HISTOGRAM ;
INSET N MEDIAN (6.2) MEAN (6.2) P95 (6.2) / 
   POS=NE HEADER="Summary Statistics" ;
TITLE "Bayesian UTL for Example 1" ;
RUN ;

