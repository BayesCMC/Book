/* Example 3 SAS Code */

/* The SAS code below generates a one-sided 95% confidence, 99% coverage upper tolerance limit for a 
   Normal distribution, using Bayesian methodology. The means are treated as random deviations from an 
   overall mean, and the standard deviation is common across samples. The data are in SAS dataset Data2. 
   The SOLUTION option is required in the PROC MIXED code, which places a sample of 10,000 
   observations from the posterior distribution into the SAS dataset Posterior3. The PTRANS option 
   is not required, but requests additional information about the model fit. The subsequent DATA 
   step calculates 10,000 observations from the posterior distribution for the 99th percentile (P99) 
   of the underlying Normal distribution. The SAS variable name for the overall mean is beta1, and the 
   SAS variable names for the variances are covp1 and covp2; these names are generated by PROC MIXED. */


DATA Data3 ;
INPUT Campaign Response ;
DATALINES ;
1  95.661
1 102.529
1 103.135
1  99.827
2  98.830
2  94.887
2 103.362
2  94.117
3  96.665
3 106.234
3 103.735
3 104.317
3 101.807
4  98.198
4  98.186
4 107.872
4  99.987
4 103.051
4 106.445
5  95.922
5 102.956
5 101.596
5  96.806
5 107.041
5  92.589
RUN ;

PROC MIXED DATA=Data3 ;
CLASS Campaign ;
MODEL Response = / SOLUTION ;
RANDOM Campaign ;
PRIOR / OUT=Posterior3 NSAMPLE=10000 SEED=6456443 PTRANS ;
TITLE "Example 3 Model Fit and Posterior Sample Generation" ;
RUN ;

PROC PRINT DATA=Posterior3 (OBS=20) ;
TITLE "First 20 Rows of Posterior3" ;
RUN ;


PROC UNIVARIATE DATA=Posterior3;
	VAR beta1 covp1 covp2;
	HISTOGRAM ;
	INSET n median (6.2) mean (6.2) min (6.2) max (6.2) 
		/ POS=ne HEADER='Summary Statistics' ;
	TITLE "Example 3 Diagnostics" ;
RUN ;


PROC SGPLOT DATA=Posterior3;
SCATTER x=sample y=beta1;
TITLE "Example 3 Diagnostics" ;
RUN ;

PROC SGPLOT DATA=Posterior3;
SCATTER x=sample y=covp1;
TITLE "Example 3 Diagnostics" ;
RUN ;

PROC SGPLOT DATA=Posterior3;
SCATTER x=sample y=covp2;
TITLE "Example 3 Diagnostics" ;
RUN ;

DATA Posterior ;
SET Posterior3 ;
SEED = 4896235 ;
z99 = PROBIT(0.99) ;
Mu = beta1 ;
SigmaTotal = SQRT(covp1 + covp2) ;
P99 = Mu + z99*SigmaTotal ;
RUN ;

PROC UNIVARIATE DATA=Posterior ;
VAR P99 ;
HISTOGRAM ;
INSET N MEDIAN (6.2) MEAN (6.2) P95 (6.2) / 
   POS=NE HEADER = "Summary Statistics" ;
TITLE "Bayesian UTL for Example 3" ;
RUN ;

