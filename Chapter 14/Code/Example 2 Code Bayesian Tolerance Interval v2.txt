/* Example 2 SAS Code */

/* The SAS code below generates one-sided 95% confidence, 99% coverage upper tolerance limits 
   for three Normal distributions with a common standard deviation, using both frequentist and 
   Bayesian methodology. The means are treated as fixed effects. The data are in SAS dataset 
   Data2. The PROC CAPABILITY code generates the frequentist UTLs, and the remainder of the 
   code generates and displays the Bayesian UTLs. The METHOD=3 option in PROC CAPABILITY specifies 
   a tolerance limit. The SOLUTION option is required in the PROC MIXED code, which places a 
   sample of 10,000 observations from the posterior distribution into the SAS dataset Posterior2. 
   The subsequent DATA step calculates 10,000 observations from the posterior distribution for the 
   99th percentiles (P99_1, P99_2 and P99_3) of the underlying Normal distributions. The SAS 
   variable names for fixed effects are beta1, beta2 and beta3, and the SAS variable name for the 
   variance is covp1; these names are generated by PROC MIXED. The DATA step code shows how beta1, 
   beta2 and beta3 are related to the three distributions’ means. */

/* Note: The SAS code for the frequentist UTLs doesn’t take advantage of the fact that the 
   underlying Normal distributions share a common standard deviation. The SAS code for the Bayesian 
   UTLs does take advantage of this fact. */


DATA Data2 ;
INPUT Material $ Response ;
DATALINES ;
A  98.237
A  91.635
A  89.295
A 103.978
A  97.524
A  86.054
A  96.447
A 101.381
A 100.291
A 104.515
B 104.021
B  95.457
B 107.248
B 102.030
B  96.970
B 103.693
B  95.692
B  90.270
B 104.381
B 102.632
C 103.487
C 112.351
C 112.765
C 109.692
C 107.885
C 104.632
C 106.856
C 114.189
C 110.046
C 101.199 
RUN ;

PROC CAPABILITY DATA=Data2 ;
BY Material ;
INTERVALS Response / TYPE=UPPER P=0.99 ALPHA=0.05 METHOD=3 ;
TITLE "Frequentist UTLs for Example 2" ;
RUN ;

PROC MIXED DATA=Data2 ;
CLASS Material ;
MODEL Response = Material / SOLUTION ;
PRIOR / OUT=Posterior2 NSAMPLE=10000 SEED=6456443 ;
TITLE "Example 2 Model Fit and Posterior Sample Generation" ;
RUN ;

PROC PRINT DATA=Posterior2 (OBS=20) ;
TITLE "First 20 Rows of Posterior2" ;
RUN ;

DATA Posterior ;
SET Posterior2 ;
SEED = 4896235 ;
z99 = PROBIT(0.99) ;
Mu1 = beta1+beta2 ;
Mu2 = beta1+beta3 ;
Mu3 = beta1 ;
Sigma = SQRT(covp1) ;
P99_1 = Mu1 + z99*Sigma ;
P99_2 = Mu2 + z99*Sigma ;
P99_3 = Mu3 + z99*Sigma ;
RUN ;


PROC UNIVARIATE DATA=Posterior;
VAR Mu1 Mu2 Mu3 covp1 ;
HISTOGRAM ;
INSET n median (6.2) mean (6.2) min (6.2) max (6.2) 
	/ POS=ne HEADER='Summary Statistics' ;
TITLE "Example 2 Diagnostics" ;
run ;


PROC SGPLOT DATA=Posterior;
SCATTER x=sample y=Mu1;
TITLE "Example 2 Diagnostics" ;
RUN;

PROC SGPLOT DATA=Posterior;
SCATTER x=sample y=Mu2;
TITLE "Example 2 Diagnostics" ;
RUN;

PROC SGPLOT DATA=Posterior;
SCATTER x=sample y=Mu3;
TITLE "Example 2 Diagnostics" ;
RUN;

PROC SGPLOT DATA=Posterior;
SCATTER x=sample y=covp1;
TITLE "Example 2 Diagnostics" ;
RUN;

PROC UNIVARIATE DATA=Posterior ;
VAR P99_1 P99_2 P99_3 ;
HISTOGRAM ;
INSET N MEDIAN (6.2) MEAN (6.2) P95 (6.2) / 
   POS=NE HEADER="Example 2 Summary Statistics" ;
TITLE "Bayesian UTLs for Example 2" ;
RUN ; 

